
plugins {
    id "nebula.os-package" version "2.2.6"
}

apply plugin: 'application'

mainClassName = "se.tre.freki.Main"
applicationName = "freki"
applicationDefaultJvmArgs = [ "-server", "-XX:+UseG1GC", "-XX:+AggressiveOpts", "-XX:+UseFastAccessorMethods" ]

dependencies {
    compile project(":freki-core")
    compile project(":freki-cassandra")

    compile project(":label-manager:freki-create")
    compile project(":freki-web")
}

run {
    systemProperties = [
        "app.home": rootDir
    ]
}

startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replaceAll('cd "\\$SAVED" \\>\\&-\n', '$0\nDEFAULT_JVM_OPTS="-Dapp.home=\\$APP_HOME"\n')
        windowsScript.text = windowsScript.text.replaceAll('\r\n@rem Find java.exe', '\r\nset DEFAULT_JVM_OPTS=-Dapp.home=%APP_HOME%\r\n$0')
    }
}

distributions {
    main {
        baseName = rootProject.name

        contents {
            into('') {
                from { project(':freki-core').distFiles }
                from { project(':freki-cassandra').distFiles }
            }
        }
    }
}

ospackage {
    packageName = rootProject.name
    os = LINUX

    into "/opt/${rootProject.name}"

    from(jar.outputs.files) {
        into 'lib'
    }

    from(configurations.runtime) {
        into 'lib'
    }

    from("${buildDir}/scripts") {
        into 'bin'
        fileMode 0750

        // These are linux packages so we do not care about the windows
        // start scripts
        exclude 'freki.bat'
    }

    from(files('src/main/dist')) {
        into ''
        fileType CONFIG | NOREPLACE
    }

    from(project(':freki-cassandra').distFiles) {
        into ''
        fileType CONFIG | NOREPLACE
    }
}

buildRpm {
    user = rootProject.name
    permissionGroup = rootProject.name

    requires('jre1.8.0_51')

    preInstall file('src/main/rpm/preInstall.sh')

    from(files('src/main/dist/init')) {
        into '/etc/init/'
    }
}
